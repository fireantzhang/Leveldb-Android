apply plugin: 'android-library'

group = project.GROUP
version = project.VERSION_NAME

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        ndk {
            moduleName "leveldbjni"
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

// query for all (non-test) variants and inject a new step in the builds
android.libraryVariants.all { variant ->
    def jarTask = project.tasks.create(name:"jar${variant.name.capitalize()}", type: Jar) {
        from            variant.javaCompile.destinationDir
        exclude         "**/R.class"
        exclude          "**/BuildConfig.class"
    }
    jarTask.dependsOn variant.javaCompile
    artifacts.add('archives', jarTask);
}

apply from: 'https://raw.github.com/googolmo/gradle-mvn-push/master/gradle-mvn-push.gradle'

